//..............................................................................
//
// Hardware Serial Monitor log representer
//

import "log_Representation.jnc"
import "HwSerialMonLogRecordCode.jnc"

//..............................................................................

char const* getLineString(uint_t line) {
	return line ? "\e[31mon\e[m" : "\e[90moff\e[m";
}

// . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

bool representHwSerialMonLog(
	log.Representation* representation,
	uint64_t recordCode,
	void const* p,
	size_t size,
	uint_t foldFlags
) {
	switch (recordCode) {
	case HwSerialMonRecordCode.CaptureStarted:
		char const* ifaceName = p;
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Info;
		representation.addHyperText($"Capture started on \e[34m$ifaceName\e[m interface");
		break;

	case HwSerialMonRecordCode.CaptureStopped:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Info;
		representation.addHyperText("Capture stopped");
		break;

	case HwSerialMonRecordCode.RawPacket:
//		representation.m_lineAttr.m_iconIdx = -1;
//		representation.m_lineAttr.m_textColor = log.StdLogColor.Rx;
		representation.addBin(p, size);
		break;

	case HwSerialMonRecordCode.UartRtsChanged:
		bool rts = *(bool const*) p;
		char const* rtsString = getLineString(rts);

		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Info;
		representation.addHyperText($"RTS changed: $rtsString");
		break;

	case HwSerialMonRecordCode.UartCtsChanged:
		bool dtr = *(bool const*) p;
		char const* ctsString = getLineString(dtr);

		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Info;
		representation.addHyperText($"CTS changed: $ctsString");
		break;

	case HwSerialMonRecordCode.UartFramingError:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Error;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Error;
		representation.addHyperText("UART framing error");
		break;

	case HwSerialMonRecordCode.UartParityError:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Error;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Error;
		representation.addHyperText("UART parity error");
		break;

	case HwSerialMonRecordCode.I2cEmptyMessage:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Warning;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Warning;
		representation.addHyperText("I\u00b2C empty message");
		break;

	case HwSerialMonRecordCode.I2cPreemptiveStart:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Warning;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Warning;
		representation.addHyperText("I\u00b2C preemptive start bit");
		break;

	case HwSerialMonRecordCode.I2cPreemptiveStop:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Error;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Error;
		representation.addHyperText("I\u00b2C preemptive stop bit");
		break;

	case HwSerialMonRecordCode.I2cCharNack:
		representation.m_lineAttr.m_textColor = log.StdLogColor.Rx;
		representation.m_lineAttr.m_fontFlags = ui.FontFlags.Strikeout;
		representation.addBin(log.StdRecordCode.Rx, p, size);
		break;

	case HwSerialMonRecordCode.SpiEmptyMessage:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Warning;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Warning;
		representation.addHyperText("SPI empty message");
		break;

	case HwSerialMonRecordCode.SpiFramingError:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Error;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Error;
		representation.addHyperText("SPI framing error");
		break;

	default:
		return false;
	}

	return true;
}

//..............................................................................
