//..............................................................................
//
// SSL Listener session
//

import "doc_Session.jnc"
import "io_base.jncx"
import "io_ssl.jncx"
import "ui_History.jnc"
import "ui_SocketUi.jnc"
import "ui_SslPropertySet.jnc"
import "ui_BufferPropertySet.jnc"
import "ui_StdSessionInfoSet.jnc"
import "ui_ListWidget.jnc"
import "log_ThroughputCalc.jnc"
import "log_ChecksumCalc.jnc"
import "std_List.jnc"
import "std_Buffer.jnc"

using extension ui.ComboBoxHistory;

//..............................................................................

class SslServerSession: doc.Session {
protected:
	enum Defaults {
		LocalPort = 8443,
	}

	enum ListenerState {
		Closed,
		Listening,
	}

	enum ConnectionState {
		Closed,
		SslHandshaking,
		Connected,
	}

	enum ActionId {
		Listen,
		Disconnect,
		DisconnectAll,
		_Count,
	}

	enum IconId {
		Listen,
		StopListen,
		_Count,
	}

	enum StatusPaneId {
		ListenerState,
		ClientCount,
		_Count,
	}

	class Client {
		std.ListEntry* m_entry;
		ConnectionState bindable m_state;
		bool bindable m_isTransmitEnabled;
		io.SslSocket* m_socket;
		io.SocketAddress m_address;
		uint_t m_syncId;
	}

protected:
	// listener socket

	ListenerState bindable m_listenerState;
	io.SslSocket m_listenerSocket;
	io.SocketAddress m_localAddress;
	uint_t m_listenerSyncId;
	std.List m_clientList;
	size_t bindable m_clientCount;
	io.SocketAddress m_currentClientAddress;
	std.Buffer m_readBuffer;

	// toolbar, status bar & menu

	ui.Action* m_actionTable[ActionId._Count];
	ui.Icon* m_iconTable[IconId._Count];
	ui.StatusPane* m_statusPaneTable[StatusPaneId._Count];
	ui.ComboBox* m_adapterCombo;
	ui.ComboBox* m_portCombo;
	ui.ListWidget* m_clientWidget;

	// property grid

	ui.SocketPropertySet m_socketPropertySet;
	ui.SslPropertySet m_sslPropertySet;
	ui.BufferPropertySet m_bufferPropertySet;

	// information grid

	ui.StdSessionInfoSet m_stdSessionInfoSet;
	ui.ThroughputInfoSet m_throughputInfoSet;
	ui.ChecksumInfoSet m_checksumInfoSet;

	// ui reactor

	reactor m_uiReactor;

public:
	construct(doc.PluginHost* pluginHost);

	bool errorcode listen();
	void stopListen();
	void disconnect();
	void disconnectAll();

	override void load(doc.Storage* storage);
	override void save(doc.Storage* storage);
	override void updateProperties();
	override bool errorcode applyProperties();
	override void restoreDefaultProperties();

	override size_t errorcode transmit(
		void const* p,
		size_t size
	);

	override void disconnectSession() {
		disconnectAll();
	}

protected:
	void onListen() {
		m_listenerState ? stopListen() : try listen();
	}

	void onPortComboEnter() {
		try listen();
	}

	void onListenerSocketEvent(
		uint_t syncId,
		io.SslSocketEvents triggeredEvents
	);

	void onClientSocketEvent(
		Client* client,
		uint_t syncId,
		io.SslSocketEvents triggeredEvents
	);

	void createUi();
	void waitListenerSocketEvents();
	void waitClientSocketEvents(Client* client);
	void disconnect(Client* client);
	void addClient(Client* client);
	void removeClient(Client* client);
	void setCurrentClient(Client* client);
}

// . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

SslServerSession.construct(doc.PluginHost* pluginHost) {
	basetype.construct(pluginHost);

	createUi();

	pluginHost.m_log.addSelectionProcessor(log.ThroughputCalcRangeProcessor.factory~(m_throughputInfoSet));
	pluginHost.m_log.addSelectionProcessor(log.ChecksumCalcRangeProcessor.factory~(m_checksumInfoSet));
}

void SslServerSession.createUi() {
	// toolbar

	m_pluginHost.m_toolBar.addLabel("Adapter:");
	m_adapterCombo = ui.createNetworkAdapterComboBox(m_pluginHost.m_toolBar, ui.NetworkAdapterUsage.Listener);

	m_pluginHost.m_toolBar.addSpacing();
	m_pluginHost.m_toolBar.addLabel("Port:");
	m_portCombo = m_pluginHost.m_toolBar.addComboBox();
	m_portCombo.m_isEditable = true;
	m_portCombo.m_onReturnPressed += onPortComboEnter;

	m_iconTable[IconId.Listen] = new ui.Icon("images/listen.png");
	m_iconTable[IconId.StopListen] = new ui.Icon("images/pause.png");
	m_actionTable[ActionId.Listen] = createAction("Listen", m_iconTable[IconId.Listen]);
	m_actionTable[ActionId.Listen].m_onTriggered += onListen;

	// form

	ui.VBoxLayout layout;
	layout.addLabel("Active client(s):");
	m_clientWidget = new ui.ListWidget;
	m_clientWidget.m_placeholderText = "No active clients";
	layout.addWidget(m_clientWidget, 1);

	m_actionTable[ActionId.Disconnect] = new ui.Action("images/disconnect.png", "Disconnect", disconnect);
	m_actionTable[ActionId.DisconnectAll] = new ui.Action("images/disconnect-all.png", "Disconnect All", disconnectAll);

	ui.ToolBar clientToolBar;
	clientToolBar.addStretch();
	clientToolBar.addAction(m_actionTable[ActionId.Disconnect]);
	clientToolBar.addAction(m_actionTable[ActionId.DisconnectAll]);
	layout.addWidget(clientToolBar);

	m_pluginHost.createForm(layout, 1);

	// status bar

	m_statusPaneTable[StatusPaneId.ListenerState] = m_pluginHost.m_statusBar.addPane(, 80);
	m_statusPaneTable[StatusPaneId.ClientCount] = m_pluginHost.m_statusBar.addPane(, 80);

	// property grid

	ui.GroupProperty* propGroup = m_pluginHost.m_propertyGrid.createGroupProperty(
		"TCP",
		"TCP-specific connection settings"
	);

	m_socketPropertySet.createProperties(
		m_pluginHost.m_propertyGrid,
		propGroup,,
		ui.SocketPropertySet.PropertyId.AllTcpServer
	);

	m_socketPropertySet.m_defaultLocalPort = Defaults.LocalPort;

	propGroup = m_pluginHost.m_propertyGrid.createGroupProperty(
		"SSL",
		"SSL-specific connection settings"
	);

	m_sslPropertySet.createProperties(
		m_pluginHost.m_propertyGrid,
		propGroup,,
		ui.SslPropertySet.PropertyId.AllServer
	);

	m_bufferPropertySet.createPropertiesGrouped(
		m_pluginHost.m_propertyGrid,,,
		ui.BufferPropertySet.PropertyId.All & ~ui.BufferPropertySet.PropertyId.ReadParallelism
	);

	// information grid

	ui.InformationGroup* infoGroup = m_pluginHost.m_informationGrid.createGroup("SSL server");

	m_stdSessionInfoSet.create(m_pluginHost.m_txRxStats, m_pluginHost.m_informationGrid, infoGroup);
	m_throughputInfoSet.createInformationValuesGrouped(m_pluginHost.m_informationGrid);
	m_checksumInfoSet.createInformationValuesGrouped(m_pluginHost.m_informationGrid);

	m_pluginHost.m_propertyGrid.resizeNameColumnToContents();
	m_pluginHost.m_informationGrid.resizeNameColumnToContents();

	m_uiReactor.start();
}

bool errorcode SslServerSession.listen() {
	stopListen();
	startSession();

	m_portCombo.addEditTextToHistory();
	io.SocketAddress address = *(io.SocketAddress const*)m_adapterCombo.m_currentData;
	address.m_port = (uint16_t)atoi(m_portCombo.m_editText);

	m_listenerSocket.open(&address);
	m_listenerSocket.listen();

	address = m_listenerSocket.m_address;
	m_logWriter.write(SocketLogRecordCode.Listening, &address, sizeof(address));
	m_localAddress = address;
	m_listenerState = ListenerState.Listening;
	waitListenerSocketEvents();
	m_stdSessionInfoSet.startSession(m_pluginHost.m_mainThreadScheduler);
	return true;

catch:
	m_logWriter.writeLastError(SocketLogRecordCode.ListenError);
	m_listenerSocket.close();
	m_listenerState = ListenerState.Closed;
	m_listenerSyncId++;
	m_stdSessionInfoSet.endSession();
	return false;
}

void SslServerSession.stopListen() {
	if (m_listenerState != ListenerState.Listening)
		return;

	m_logWriter.write(SocketLogRecordCode.ListenStopped, &m_localAddress, sizeof(m_localAddress));
	m_listenerSocket.close();
	m_listenerState = ListenerState.Closed;
	m_listenerSyncId++;
	m_stdSessionInfoSet.endSession();
}

void SslServerSession.disconnect() {
	Client* client = m_clientWidget.m_currentData;
	if (client)
		disconnect(client);
}

void SslServerSession.disconnectAll() {
	while (m_clientList.m_count)
		disconnect(m_clientList.m_head.m_data);
}

void SslServerSession.disconnect(SslServerSession.Client* client) {
	TcpDisconnectLogRecordParams params;
	params.m_address = client.m_address;
	params.m_flags = 0;

	if (m_socketPropertySet.m_tcpResetProp.m_value)
		params.m_flags |= TcpDisconnectLogRecordFlags.Reset;

	m_logWriter.write(SocketLogRecordCode.ClientDisconnected, &params, sizeof(params));
	removeClient(client);
}

void SslServerSession.addClient(SslServerSession.Client* client) {
	client.m_entry = m_clientList.add(client);
	m_clientWidget.addItemSetCurrent(client.m_address.getString(), client);
	m_clientCount = m_clientList.m_count;
	m_currentClientAddress = client.m_address;
}

void SslServerSession.removeClient(SslServerSession.Client* client) {
	client.m_socket.close();
	client.m_syncId++;

	size_t i = m_clientWidget.findData(client);
	if (i != -1)
		m_clientWidget.removeItem(i);

	m_clientList.remove(client.m_entry);
	m_clientCount = m_clientList.m_count;
	m_currentClientAddress = null;
}

void SslServerSession.setCurrentClient(SslServerSession.Client* client) {
	if (m_currentClientAddress.isEqual(client.m_address))
		return;

	m_logWriter.write(
		SocketLogRecordCode.ClientChanged,
		&client.m_address,
		sizeof(client.m_address)
	);

	m_currentClientAddress = client.m_address;
}

void SslServerSession.load(doc.Storage* storage) {
	m_portCombo.loadHistory(storage, "portHistory");
	m_adapterCombo.m_currentIndex  = storage.readInt("adapterIdx");
	m_portCombo.m_editText = storage.readString("port");

	m_socketPropertySet.load(storage);
	m_sslPropertySet.load(storage);
	m_bufferPropertySet.load(storage);
	m_readBuffer.setSize(m_bufferPropertySet.m_readBufferSizeProp.m_value);
}

void SslServerSession.save(doc.Storage* storage) {
	m_socketPropertySet.save(storage);
	m_sslPropertySet.save(storage);
	m_bufferPropertySet.save(storage);

	m_portCombo.saveHistory(storage, "portHistory");
	storage.writeInt("adapterIdx", (int) m_adapterCombo.m_currentIndex);
	storage.writeString("port", m_portCombo.m_editText);
}

void SslServerSession.updateProperties() {
	m_socketPropertySet.m_localPortProp.m_value = atoi(m_portCombo.m_editText);
	m_socketPropertySet.m_adapterProp.m_currentIndex = m_adapterCombo.m_currentIndex;
}

bool errorcode SslServerSession.applyProperties() {
	m_portCombo.m_editText = $"$(m_socketPropertySet.m_localPortProp.m_value)";
	m_adapterCombo.m_currentIndex = m_socketPropertySet.m_adapterProp.m_currentIndex;
	m_readBuffer.setSize(m_bufferPropertySet.m_readBufferSizeProp.m_value);

	std.ListEntry* entry = m_clientList.m_head;
	for (; entry; entry = entry.m_next) {
		Client* connection = entry.m_data;
		connection.m_socket.m_readBlockSize = m_bufferPropertySet.m_readBlockSizeProp.m_value;
		connection.m_socket.m_readBufferSize = m_bufferPropertySet.m_readBufferSizeProp.m_value;
		connection.m_socket.m_writeBufferSize = m_bufferPropertySet.m_writeBufferSizeProp.m_value;
		connection.m_socket.m_options = ui.getSocketOptions(m_socketPropertySet, m_bufferPropertySet);
	}

	return true;
}

void SslServerSession.restoreDefaultProperties() {
	m_socketPropertySet.restoreDefaults();
	m_sslPropertySet.restoreDefaults();
	m_bufferPropertySet.restoreDefaults();
}

size_t errorcode SslServerSession.transmit(
	void const* p,
	size_t size
) {
	Client* client = m_clientWidget.m_currentData;
	if (!client) {
		std.setError("not connected");
		return -1;
	}

	setCurrentClient(client);

	size_t result = client.m_socket.write(p, size);
	if (result)
		m_logWriter.write(log.StdRecordCode.Tx, p, result);

	if (result < size && client.m_isTransmitEnabled) {
		client.m_isTransmitEnabled = false;
		waitClientSocketEvents(client);
	}

	return result;
}

void SslServerSession.waitListenerSocketEvents() {
	m_listenerSocket.wait(
		io.SslSocketEvents.IoError |
		io.SslSocketEvents.IncomingConnection,
		onListenerSocketEvent~(++m_listenerSyncId) @ m_pluginHost.m_mainThreadScheduler
	);
}

void SslServerSession.waitClientSocketEvents(SslServerSession.Client* client) {
	io.SslSocketEvents eventMask = io.SslSocketEvents.IoError;

	switch (client.m_state) {
	case ConnectionState.SslHandshaking:
		eventMask |=
			io.SslSocketEvents.TcpDisconnected |
			io.SslSocketEvents.TcpReset |
			io.SslSocketEvents.SslHandshakeCompleted;
		break;

	case ConnectionState.Connected:
		eventMask |=
			io.SslSocketEvents.IncomingData |
			io.SslSocketEvents.TcpDisconnected |
			io.SslSocketEvents.TcpReset;

		if (!client.m_isTransmitEnabled)
			eventMask |= io.SslSocketEvents.WriteBufferReady;

		break;
	}

	client.m_socket.wait(
		eventMask,
		onClientSocketEvent~(client, ++client.m_syncId) @ m_pluginHost.m_mainThreadScheduler
	);
}

void SslServerSession.onListenerSocketEvent(
	uint_t syncId,
	io.SslSocketEvents triggeredEvents
) {
	if (syncId != m_listenerSyncId)
		return;

	if (triggeredEvents & io.SslSocketEvents.IoError) {
		m_logWriter.write(log.StdRecordCode.Error, m_listenerSocket.m_ioError);
		m_listenerSocket.close();
		m_listenerState = ListenerState.Closed;
		m_stdSessionInfoSet.endSession();
		return;
	}

	if (triggeredEvents & io.SslSocketEvents.IncomingConnection) {
		io.SslSocket* socket;

	nestedscope:
		io.SocketAddress address;
		socket = m_listenerSocket.accept(&address, true);
		m_logWriter.write(SocketLogRecordCode.ClientConnected, &address, sizeof(address));
		m_logWriter.write(SslLogRecordCode.SslHandshaking);

		socket.m_readBlockSize = m_bufferPropertySet.m_readBlockSizeProp.m_value;
		socket.m_readBufferSize = m_bufferPropertySet.m_readBufferSizeProp.m_value;
		socket.m_writeBufferSize = m_bufferPropertySet.m_writeBufferSizeProp.m_value;
		socket.m_options = ui.getSocketOptions(m_socketPropertySet, m_bufferPropertySet);

		socket.m_securityLevel = m_sslPropertySet.m_securityLevelProp.m_value;
		socket.enableCiphers(m_sslPropertySet.m_cipherSetProp.m_value);

		if (m_sslPropertySet.m_useEphemeralDhProp.m_value)
			socket.setEphemeralDhStdParams(m_sslPropertySet.m_ephemeralDhStdParamsProp.m_value);

		if (m_sslPropertySet.m_useEphemeralEcdhProp.m_value)
			socket.setEphemeralEcdhCurve(m_sslPropertySet.m_ephemeralEcdhCurveProp.m_value);

		if (m_sslPropertySet.m_useCertificateProp.m_value) {
			socket.loadCertificate(m_sslPropertySet.m_certificateFileProp.m_value);
			socket.loadPrivateKey(m_sslPropertySet.m_privateKeyFileProp.m_value);

			if (m_sslPropertySet.m_verifyCertificateProp.m_value) {
				socket.m_verifyMode = io.SslVerifyMode.VerifyPeerCertificate;
				socket.m_verifyDepth = m_sslPropertySet.m_verifyDepthProp.m_value;

				socket.loadVerifyLocations(
					m_sslPropertySet.m_verifyCaFileProp.m_value,
					m_sslPropertySet.m_verifyCaDirProp.m_value
				);
			}
		}

		socket.unsuspend();

		Client* client = new Client;
		client.m_state = ConnectionState.SslHandshaking;
		client.m_socket = socket;
		client.m_address = address;

		addClient(client);
		waitClientSocketEvents(client);

	catch:
		m_logWriter.writeLastError(SocketLogRecordCode.ConnectError);

		if (socket)
			socket.close();
	}

	waitListenerSocketEvents();
}

void SslServerSession.onClientSocketEvent(
	SslServerSession.Client* client,
	uint_t syncId,
	io.SslSocketEvents triggeredEvents
) {
	if (syncId != client.m_syncId)
		return;

	if (triggeredEvents & io.SslSocketEvents.IoError) {
		setCurrentClient(client);
		m_logWriter.write(log.StdRecordCode.Error, client.m_socket.m_ioError);
		removeClient(client);
		return;
	}

	if (triggeredEvents & io.SslSocketEvents.SslHandshakeCompleted) {
		m_logWriter.write(SslLogRecordCode.SslHandshakeCompleted);

		std.Buffer buffer;
		size_t count = client.m_socket.m_peerCertificateChainLength;
		for (size_t i = 0; i < count; i++) {
			client.m_socket.m_peerCertificateChain[i].encode(buffer, io.SslCertFormat.Der);
			m_logWriter.write(SslLogRecordCode.SslCertificate, buffer.m_p, buffer.m_size);
		}

		io.SslCertificate const* cert = client.m_socket.m_peerCertificate;
		if (cert) {
			cert.encode(buffer, io.SslCertFormat.Der);
			m_logWriter.write(SslLogRecordCode.SslCertificate, buffer.m_p, buffer.m_size);
		}

		m_logWriter.write(SslLogRecordCode.SslCipher, client.m_socket.m_currentCipher.m_name);

		m_logWriter.write(SslLogRecordCode.SslConnectCompleted);
		client.m_state = ConnectionState.Connected;
		client.m_isTransmitEnabled = true;
		m_stdSessionInfoSet.startSession(m_pluginHost.m_mainThreadScheduler);
	}

	if (triggeredEvents & io.SslSocketEvents.TcpDisconnected) {
		for (;;) { // read all the buffered data
			size_t result = client.m_socket.read(m_readBuffer.m_p, m_readBuffer.m_size);
			if (!result)
				break;

			setCurrentClient(client);
			m_logWriter.write(log.StdRecordCode.Rx, m_readBuffer.m_p, result);
		}

		TcpDisconnectLogRecordParams recordParams;
		recordParams.m_address = client.m_address;
		recordParams.m_flags = TcpDisconnectLogRecordFlags.Remote;

		if (triggeredEvents & io.SslSocketEvents.TcpReset)
			recordParams.m_flags |= TcpDisconnectLogRecordFlags.Reset;

		m_logWriter.write(
			SocketLogRecordCode.ClientDisconnected,
			&recordParams,
			sizeof(recordParams)
		);

		removeClient(client);
		return;
	}

	if (triggeredEvents & io.SslSocketEvents.IncomingData) {
		setCurrentClient(client);
		m_clientWidget.m_currentData = client;

		size_t result = client.m_socket.read(m_readBuffer.m_p, m_readBuffer.m_size);
		if (result)
			m_logWriter.write(log.StdRecordCode.Rx, m_readBuffer.m_p, result);
	}

	if (triggeredEvents & io.SocketEvents.WriteBufferReady)
		client.m_isTransmitEnabled = true;

	waitClientSocketEvents(client);
}

reactor SslServerSession.m_uiReactor {
	m_title = $"SSL @ $(m_portCombo.m_editText)";
	m_actionTable[ActionId.Listen].m_text = m_listenerState ? "Stop Listening" : "Listen";
	m_actionTable[ActionId.Listen].m_icon = m_iconTable[m_listenerState ? IconId.StopListen : IconId.Listen];
	m_clientWidget.m_isEnabled =
	m_actionTable[ActionId.Disconnect].m_isEnabled =
	m_actionTable[ActionId.DisconnectAll].m_isEnabled = m_clientCount;
	m_statusPaneTable[StatusPaneId.ListenerState].m_text = m_listenerState ? "Listening" : "Not listening";
	m_statusPaneTable[StatusPaneId.ClientCount].m_text = $"$m_clientCount client(s)";

	m_isTransmitEnabled =
		m_clientWidget.m_currentData &&
		((Client*)m_clientWidget.m_currentData).m_isTransmitEnabled;

	m_sessionConnectionState = m_clientWidget.m_currentData ?
		SessionConnectionState.Connected :
		SessionConnectionState.Disconnected;
}

//..............................................................................
