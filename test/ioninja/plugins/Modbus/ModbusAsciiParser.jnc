//..............................................................................
//
// Modbus ASCII stream parser
//

import "hexEncoding.jnc"

class ModbusAsciiParser: ModbusParserBase {
protected:
	enum State {
		Idle,
		Adu,
		Cr,
	}

protected:
	State m_state;
	std.Buffer m_asciiBuffer;
	std.Buffer m_binBuffer;

public:
	construct(ModbusInfoSet* infoSet) {
		basetype.construct(io.ModbusProtocol.Ascii, infoSet);
	}

	override parse(
		log.Writer* writer,
		uint64_t timestamp,
		uint64_t originalRecordCode,
		void const* p,
		size_t size
	);

	override void reset() {
		basetype.reset();
		m_state = State.Idle;
	}

protected:
	ModbusLogRecordCode getPacketRecordCode() {
		return m_modbusRole == io.ModbusRole.Master ?
			ModbusLogRecordCode.Packet_ascii_master :
			ModbusLogRecordCode.Packet_ascii_slave;
	}
}

// . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

void ModbusAsciiParser.parse(
	log.Writer* writer,
	uint64_t timestamp,
	uint64_t originalRecordCode,
	void const* p,
	size_t size
) {
	updateLastTimestamp(timestamp);

	ModbusLogRecordCode recordCode = getPacketRecordCode();

	void const* p0 = p;
	void const* adu = p;
	void const* end = p + size;
	while (p < end) {
		switch (m_state) {
		case State.Idle:
			p = memchr(p, ':', end - p);
			if (!p)
				break2;

			m_state = State.Adu;
			m_asciiBuffer.clear();
			adu = ++p;
			break;

		case State.Adu:
			p = memchr(p, '\r', end - p);
			if (!p)
				break2;

			m_state = State.Cr;
			p++;
			break;

		case State.Cr:
			m_asciiBuffer.append(adu, p - adu);

			if (*(char const*)p == '\n')
				p++;
			else {
				std.setError("missing '\\n' in Modbus ASCII frame");
				writer.writeLastError(timestamp, ModbusLogRecordCode.ParseError);
				m_infoSet.addError();
			}

			if (p0 < p)
				writer.write(timestamp, originalRecordCode, p0, p - p0);

			p0 = p;

			decodeHexString(&m_binBuffer, string_t(m_asciiBuffer.m_p, m_asciiBuffer.m_size));
			writer.write(timestamp, recordCode, m_binBuffer.m_p, m_binBuffer.m_size);
			m_infoSet.addAdu(m_modbusRole);

			if (m_mode == ModbusParserMode.Alternate) {
				m_modbusRole = (io.ModbusRole)!m_modbusRole;
				recordCode = getPacketRecordCode();
			}

			m_state = State.Idle;
			break;
		}
	}

	if (p0 < end)
		writer.write(timestamp, originalRecordCode, p0, end - p0);

	if (m_state == State.Adu && adu < end) {
		m_asciiBuffer.append(adu, end - adu);
		if (m_asciiBuffer.m_size > io.ModbusMaxAsciiAduSize) {
			std.setError("Modbus ASCII frame is too big and still incomplete");
			writer.writeLastError(timestamp, ModbusLogRecordCode.ParseError);
			m_infoSet.addError();
			reset();
		}
	}
}

//..............................................................................
