//..............................................................................
//
// TCP Listener session
//

import "doc_Session.jnc"
import "io_base.jncx"
import "io_Socket.jnc"
import "ui_History.jnc"
import "ui_SocketUi.jnc"
import "ui_BufferPropertySet.jnc"
import "ui_StdSessionInfoSet.jnc"
import "log_ThroughputCalc.jnc"
import "log_ChecksumCalc.jnc"
import "std_Buffer.jnc"
import "std_List.jnc"

using extension ui.ComboBoxHistory;

//..............................................................................

class TcpServerSession: doc.Session {
protected:
	enum Defaults {
		LocalPort = 8080,
	}

	enum State {
		Closed,
		Listening,
	}

	enum ActionId {
		Listen,
		Disconnect,
		DisconnectAll,
		ApplyFilter,
		_Count,
	}

	enum IconId {
		Listen,
		StopListen,
		_Count,
	}

	enum StatusPaneId {
		ListenerState,
		ClientCount,
		_Count,
	}

	class Client {
		std.ListEntry* m_entry;
		bool bindable m_isTransmitEnabled;
		io.Socket* m_socket;
		io.SocketAddress m_address;
		uint_t m_syncId;
	}

protected:
	// listener socket & list of clients

	State bindable m_state;
	io.Socket m_listenerSocket;
	io.SocketAddress m_localAddress;
	uint_t m_listenerSyncId;
	std.List m_clientList;
	size_t bindable m_clientCount;
	Client* m_currentClient;
	std.Buffer m_readBuffer;
	SocketClientLogFilter m_filter;

	// toolbar, status bar & menu

	ui.Action* m_actionTable[ActionId._Count];
	ui.Icon* m_iconTable[IconId._Count];
	ui.StatusPane* m_statusPaneTable[StatusPaneId._Count];
	ui.ComboBox* m_adapterCombo;
	ui.ComboBox* m_portCombo;
	ui.ComboBox* m_clientCombo;
	ui.ComboBox* m_clientFilterCombo;

	// property grid

	ui.SocketPropertySet m_socketPropertySet;
	ui.StringProperty* m_clientFilterProp;
	ui.BufferPropertySet m_bufferPropertySet;

	// information grid

	ui.StdSessionInfoSet m_stdSessionInfoSet;
	ui.ThroughputInfoSet m_throughputInfoSet;
	ui.ChecksumInfoSet m_checksumInfoSet;

	// ui reactor

	reactor m_uiReactor;

public:
	construct(doc.PluginHost* pluginHost);

	bool errorcode listen();
	void stopListen();
	void disconnect();
	void disconnectAll();

	override void load(doc.Storage* storage);
	override void save(doc.Storage* storage);
	override void updateProperties();
	override bool errorcode applyProperties();
	override void restoreDefaultProperties();

	override size_t errorcode transmit(
		void const* p,
		size_t size
	);

	override void disconnectSession() {
		disconnectAll();
	}

	override bool errorcode dispatch(
		uint_t code,
		variant in,
		variant* out
	);

protected:
	void onListen() {
		m_state ? stopListen() : try listen();
	}

	void onPortComboEnter() {
		try listen();
	}

	void onSetClientFilter() {
		try applyClientFilter(true);
	}

	void onListenerSocketEvent(
		uint_t syncId,
		io.SocketEvents triggeredEvents
	);

	void onClientSocketEvent(
		Client* client,
		uint_t syncId,
		io.SocketEvents triggeredEvents
	);

	bool errorcode applyClientFilter(bool rebuildLog);

	void createUi();
	void waitListenerSocketEvents();
	void waitClientSocketEvents(Client* client);
	void disconnect(Client* client);
	void removeClient(Client* client);
	void setCurrentClient(Client* client);
}

// . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

TcpServerSession.construct(doc.PluginHost* pluginHost) {
	basetype.construct(pluginHost);

	createUi();

	pluginHost.m_log.addSelectionProcessor(log.ThroughputCalcRangeProcessor.factory~(m_throughputInfoSet));
	pluginHost.m_log.addSelectionProcessor(log.ChecksumCalcRangeProcessor.factory~(m_checksumInfoSet));
	pluginHost.m_log.addFilter(m_filter);
}

void TcpServerSession.createUi() {
	// toolbar

	m_pluginHost.m_toolBar.addLabel("Filter:");
	m_clientFilterCombo = m_pluginHost.m_toolBar.addComboBox(-1);
	m_clientFilterCombo.m_isEditable = true;
	m_clientFilterCombo.m_onEnter += onSetClientFilter;
	m_clientFilterCombo.m_placeholderText = "Enter client address to filter...";
	m_clientFilterCombo.m_toolTipText = "View only communications with the specified client";

	m_actionTable[ActionId.ApplyFilter] = createAction("Apply filter", "images/apply.png");
	m_actionTable[ActionId.ApplyFilter].m_onTriggered += onSetClientFilter;
	m_pluginHost.m_toolBar.addSeparator();

	m_pluginHost.m_toolBar.addLabel("Client:");
	m_clientCombo = m_pluginHost.m_toolBar.addComboBox(-1);

	m_actionTable[ActionId.Disconnect] = createAction("Disconnect", "images/disconnect.png");
	m_actionTable[ActionId.Disconnect].m_onTriggered += disconnect;
	m_actionTable[ActionId.DisconnectAll] = createAction("Disconnect All", "images/disconnect-all.png");
	m_actionTable[ActionId.DisconnectAll].m_onTriggered += disconnectAll;
	m_pluginHost.m_toolBar.addSeparator();

	m_pluginHost.m_toolBar.addLabel("Adapter:");
	m_adapterCombo = ui.createNetworkAdapterComboBox(m_pluginHost.m_toolBar, ui.NetworkAdapterUsage.Listener);

	m_pluginHost.m_toolBar.addLabel("Port:");
	m_portCombo = m_pluginHost.m_toolBar.addComboBox();
	m_portCombo.m_isEditable = true;
	m_portCombo.m_onEnter += onPortComboEnter;

	m_iconTable[IconId.Listen] = new ui.Icon("images/listen.png");
	m_iconTable[IconId.StopListen] = new ui.Icon("images/pause.png");
	m_actionTable[ActionId.Listen] = createAction("Listen", m_iconTable [IconId.Listen]);
	m_actionTable[ActionId.Listen].m_onTriggered += onListen;

	// status bar

	m_statusPaneTable[StatusPaneId.ListenerState] = m_pluginHost.m_statusBar.addPane(, 80);
	m_statusPaneTable[StatusPaneId.ClientCount] = m_pluginHost.m_statusBar.addPane(, 80);

	// property grid

	ui.GroupProperty* group = m_pluginHost.m_propertyGrid.createGroupProperty(
		"TCP connection",
		"TCP-specific connection settings"
	);

	m_socketPropertySet.createProperties(
		m_pluginHost.m_propertyGrid,
		group,,
		ui.SocketPropertySet.PropertyId.AllTcpServer
	);

	m_socketPropertySet.m_defaultLocalPort = Defaults.LocalPort;

	m_bufferPropertySet.createPropertiesGrouped(m_pluginHost.m_propertyGrid);

	// information grid

	ui.InformationGroup* infoGroup = m_pluginHost.m_informationGrid.createGroup("TCP server");

	m_stdSessionInfoSet.create(m_pluginHost.m_txRxStats, m_pluginHost.m_informationGrid, infoGroup);
	m_throughputInfoSet.createInformationValuesGrouped(m_pluginHost.m_informationGrid);
	m_checksumInfoSet.createInformationValuesGrouped(m_pluginHost.m_informationGrid);

	m_pluginHost.m_propertyGrid.resizeNameColumnToContents();
	m_pluginHost.m_informationGrid.resizeNameColumnToContents();

	m_uiReactor.start();
}

bool errorcode TcpServerSession.listen() {
	stopListen();
	startSession();

	m_portCombo.addEditTextToHistory();
	io.SocketAddress address = *(io.SocketAddress const*) m_adapterCombo.m_currentData;
	address.m_port = (uint16_t)atoi(m_portCombo.m_editText);

	m_listenerSocket.open(io.Protocol.Tcp, &address);
	m_listenerSocket.listen();

	address = m_listenerSocket.m_address;
	m_logWriter.write(SocketLogRecordCode.Listening, &address, sizeof(address));
	m_localAddress = address;
	m_state = State.Listening;
	waitListenerSocketEvents();
	m_stdSessionInfoSet.startSession(m_pluginHost.m_mainThreadScheduler);
	return true;

catch:
	m_logWriter.writeLastError(SocketLogRecordCode.ListenError);
	m_listenerSocket.close();
	m_state = State.Closed;
	m_listenerSyncId++;
	m_stdSessionInfoSet.endSession();
	return false;
}

void TcpServerSession.stopListen() {
	if (m_state != State.Listening)
		return;

	m_logWriter.write(SocketLogRecordCode.ListenStopped, &m_localAddress, sizeof(m_localAddress));
	m_listenerSocket.close();
	m_state = State.Closed;
	m_listenerSyncId++;
	m_stdSessionInfoSet.endSession();
}

void TcpServerSession.disconnect() {
	Client* client = m_clientCombo.m_currentData;
	if (client)
		disconnect(client);
}

void TcpServerSession.disconnectAll() {
	while (m_clientList.m_count)
		disconnect(m_clientList.m_head.m_data);
}

void TcpServerSession.disconnect(TcpServerSession.Client* client) {
	TcpDisconnectLogRecordParams params;
	params.m_address = client.m_address;
	params.m_flags = 0;

	if (m_socketPropertySet.m_tcpResetProp.m_value)
		params.m_flags |= TcpDisconnectLogRecordFlags.Reset;

	m_logWriter.write(SocketLogRecordCode.ClientDisconnected, &params, sizeof(params));
	removeClient(client);
}

void TcpServerSession.removeClient(TcpServerSession.Client* client) {
	client.m_socket.close();
	client.m_syncId++;

	m_clientList.remove(client.m_entry);

	for (size_t i = 0; i < m_clientCombo.m_itemCount; i++)
		if (m_clientCombo.m_itemData[i] == client) {
			m_clientCombo.removeItem(i);
			break;
		}

	m_clientCount = m_clientList.m_count;
	m_currentClient = null;
}

void TcpServerSession.setCurrentClient(TcpServerSession.Client* client) {
	if (client == m_currentClient)
		return;

	m_logWriter.write(
		SocketLogRecordCode.ClientChanged,
		&client.m_address,
		sizeof(client.m_address)
	);

	m_currentClient = client;
}

bool errorcode TcpServerSession.applyClientFilter(bool rebuildLog) {
	m_pluginHost.m_log.suspend();
	bool result = try m_filter.setFilter(m_clientFilterCombo.m_editText);
	m_pluginHost.m_log.resume();

	if (!result) {
		m_clientFilterCombo.m_backgroundColor = ui.StdColor.PastelRed;
		return false;
	}

	m_clientFilterCombo.addEditTextToHistory();
	m_clientFilterCombo.m_backgroundColor = ui.ColorFlags.Undefined;

	if (rebuildLog)
		m_pluginHost.m_log.rebuild();

	return true;
}

void TcpServerSession.load(doc.Storage* storage) {
	m_portCombo.loadHistory(storage, "portHistory");
	m_adapterCombo.m_currentIndex  = storage.readInt("adapterIdx");
	m_portCombo.m_editText = storage.readString("port");

	m_socketPropertySet.load(storage);
	m_bufferPropertySet.load(storage);
	m_readBuffer.setSize(m_bufferPropertySet.m_readBufferSizeProp.m_value);
}

void TcpServerSession.save(doc.Storage* storage) {
	m_socketPropertySet.save(storage);
	m_bufferPropertySet.save(storage);

	m_portCombo.saveHistory(storage, "portHistory");
	storage.writeInt("adapterIdx", (int) m_adapterCombo.m_currentIndex);
	storage.writeString("port", m_portCombo.m_editText);
}

void TcpServerSession.updateProperties() {
	m_socketPropertySet.m_localPortProp.m_value = atoi(m_portCombo.m_editText);
	m_socketPropertySet.m_adapterProp.m_currentIndex = m_adapterCombo.m_currentIndex;
}

bool errorcode TcpServerSession.applyProperties() {
	m_portCombo.m_editText = $"$(m_socketPropertySet.m_localPortProp.m_value)";
	m_adapterCombo.m_currentIndex = m_socketPropertySet.m_adapterProp.m_currentIndex;
	m_readBuffer.setSize(m_bufferPropertySet.m_readBufferSizeProp.m_value);

	std.ListEntry* entry = m_clientList.m_head;
	for (; entry; entry = entry.m_next) {
		Client* client = entry.m_data;
		client.m_socket.m_readParallelism = m_bufferPropertySet.m_readParallelismProp.m_value;
		client.m_socket.m_readBlockSize = m_bufferPropertySet.m_readBlockSizeProp.m_value;
		client.m_socket.m_readBufferSize = m_bufferPropertySet.m_readBufferSizeProp.m_value;
		client.m_socket.m_writeBufferSize = m_bufferPropertySet.m_writeBufferSizeProp.m_value;
		client.m_socket.m_options = ui.getSocketOptions(m_socketPropertySet, m_bufferPropertySet);
	}

	return true;
}

void TcpServerSession.restoreDefaultProperties() {
	m_socketPropertySet.restoreDefaults();
	m_bufferPropertySet.restoreDefaults();
}

size_t errorcode TcpServerSession.transmit(
	void const* p,
	size_t size
) {
	Client* client = m_clientCombo.m_currentData;
	if (!client) {
		std.setError("not connected");
		return -1;
	}

	setCurrentClient(client);

	size_t result = client.m_socket.write(p, size);
	if (result)
		m_logWriter.write(log.StdRecordCode.Tx, p, result);

	if (result < size && client.m_isTransmitEnabled) {
		client.m_isTransmitEnabled = false;
		waitClientSocketEvents(client);
	}

	return result;
}

bool errorcode TcpServerSession.dispatch(
	uint_t code,
	variant in,
	variant* out
) {
	switch (code) {
	case TcpServerDispatchCode.GetLocalPort:
		*out = atoi(m_portCombo.m_editText);
		break;

	case TcpServerDispatchCode.SetLocalPort:
		m_portCombo.m_editText = $"%d"((uint_t)in);
		break;

	case TcpServerDispatchCode.Listen:
		return listen();

	case TcpServerDispatchCode.StopListen:
		stopListen();
		break;

	case TcpServerDispatchCode.Disconnect:
		disconnect();
		break;

	case TcpServerDispatchCode.DisconnectAll:
		disconnectAll();
		break;

	default:
		std.setError("unsupported dispatch code");
		return false;
	}

	return true;
}

void TcpServerSession.waitListenerSocketEvents() {
	m_listenerSocket.wait(
		io.SocketEvents.IoError |
		io.SocketEvents.IncomingConnection,
		onListenerSocketEvent~(++m_listenerSyncId) @ m_pluginHost.m_mainThreadScheduler
	);
}

void TcpServerSession.waitClientSocketEvents(TcpServerSession.Client* client) {
	io.SocketEvents eventMask =
		io.SocketEvents.IoError |
		io.SocketEvents.IncomingData |
		io.SocketEvents.TcpDisconnected |
		io.SocketEvents.TcpReset;

	if (!client.m_isTransmitEnabled)
		eventMask |= io.SocketEvents.WriteBufferReady;

	client.m_socket.wait(
		eventMask,
		onClientSocketEvent~(client, ++client.m_syncId) @ m_pluginHost.m_mainThreadScheduler
	);
}

void TcpServerSession.onListenerSocketEvent(
	uint_t syncId,
	io.SocketEvents triggeredEvents
) {
	if (syncId != m_listenerSyncId)
		return;

	if (triggeredEvents & io.SocketEvents.IoError) {
		m_logWriter.write(log.StdRecordCode.Error, m_listenerSocket.m_ioError);
		m_listenerSocket.close();
		m_state = State.Closed;
		m_stdSessionInfoSet.endSession();
		return;
	}

	if (triggeredEvents & io.SocketEvents.IncomingConnection) {
		io.SocketAddress address;
		io.Socket* socket = m_listenerSocket.accept(&address, true);
		m_logWriter.write(SocketLogRecordCode.ClientConnected, &address, sizeof(address));

		socket.m_readParallelism = m_bufferPropertySet.m_readParallelismProp.m_value;
		socket.m_readBlockSize = m_bufferPropertySet.m_readBlockSizeProp.m_value;
		socket.m_readBufferSize = m_bufferPropertySet.m_readBufferSizeProp.m_value;
		socket.m_writeBufferSize = m_bufferPropertySet.m_writeBufferSizeProp.m_value;
		socket.m_options = ui.getSocketOptions(m_socketPropertySet, m_bufferPropertySet);
		socket.unsuspend();

		Client* client = new Client;
		client.m_socket = socket;
		client.m_address = address;
		client.m_isTransmitEnabled = true;
		client.m_entry = m_clientList.add(client);
		m_clientCombo.addItemSetCurrent(client.m_address.getString(), client);
		m_clientCount = m_clientList.m_count;
		m_currentClient = client;

		waitClientSocketEvents(client);
	}

	waitListenerSocketEvents();
}

void TcpServerSession.onClientSocketEvent(
	TcpServerSession.Client* client,
	uint_t syncId,
	io.SocketEvents triggeredEvents
) {
	if (syncId != client.m_syncId)
		return;

	if (triggeredEvents & io.SocketEvents.IoError) {
		setCurrentClient(client);
		m_logWriter.write(log.StdRecordCode.Error, client.m_socket.m_ioError);
		removeClient(client);
		return;
	}

	if (triggeredEvents & io.SocketEvents.TcpDisconnected) {
		for (;;) { // read all the buffered data
			size_t result = client.m_socket.read(m_readBuffer.m_p, m_readBuffer.m_size);
			if (!result)
				break;

			setCurrentClient(client);
			m_logWriter.write(log.StdRecordCode.Rx, m_readBuffer.m_p, result);
		}

		TcpDisconnectLogRecordParams recordParams;
		recordParams.m_address = client.m_address;
		recordParams.m_flags = TcpDisconnectLogRecordFlags.Remote;

		if (triggeredEvents & io.SocketEvents.TcpReset)
			recordParams.m_flags |= TcpDisconnectLogRecordFlags.Reset;

		m_logWriter.write(
			SocketLogRecordCode.ClientDisconnected,
			&recordParams,
			sizeof(recordParams)
		);

		removeClient(client);
		return;
	}

	if (triggeredEvents & io.SocketEvents.IncomingData) {
		setCurrentClient(client);
		m_clientCombo.m_currentData = client;

		size_t result = client.m_socket.read(m_readBuffer.m_p, m_readBuffer.m_size);
		if (result)
			m_logWriter.write(log.StdRecordCode.Rx, m_readBuffer.m_p, result);
	}

	if (triggeredEvents & io.SocketEvents.WriteBufferReady)
		client.m_isTransmitEnabled = true;

	waitClientSocketEvents(client);
}

reactor TcpServerSession.m_uiReactor {
	m_title = $"TCP @ $(m_portCombo.m_editText)";
	m_actionTable[ActionId.Listen].m_text = m_state ? "Stop Listening" : "Listen";
	m_actionTable[ActionId.Listen].m_icon = m_iconTable[m_state ? IconId.StopListen : IconId.Listen];
	m_actionTable[ActionId.Disconnect].m_isEnabled =
	m_actionTable[ActionId.DisconnectAll].m_isEnabled = m_clientCombo.m_currentData;
	m_statusPaneTable[StatusPaneId.ListenerState].m_text = m_state ? "Listening" : "Not listening";
	m_statusPaneTable[StatusPaneId.ClientCount].m_text = $"$m_clientCount client(s)";
	m_clientCombo.m_isEnabled = m_clientCount;

	m_isTransmitEnabled =
		m_clientCombo.m_currentData &&
		((Client*)m_clientCombo.m_currentData).m_isTransmitEnabled;

	m_sessionConnectionState = m_clientCombo.m_currentData ?
		SessionConnectionState.Connected :
		SessionConnectionState.Disconnected;
}

//..............................................................................
