//..............................................................................
//
// Serial session
//

import "doc_Session.jnc"
import "io_base.jncx"
import "io_Serial.jnc"
import "iox_SshChannel.jnc"
import "io_HwcProto.jnc"
import "ui_History.jnc"
import "ui_SerialUi.jnc"
import "ui_BufferPropertySet.jnc"
import "ui_StdSessionInfoSet.jnc"
import "ui_GroupBox.jnc"
import "log_ThroughputCalc.jnc"
import "log_ChecksumCalc.jnc"
import "std_Buffer.jnc"

using extension ui.ComboBoxHistory;

//..............................................................................

class SshSerialSession: doc.Session {
protected:
	alias State = iox.SshChannel.State;

	enum Defaults {
		ServerPort     = 22,
	}

	enum ActionId {
		Connect,
		_Count,
	}

	enum IconId {
		Connect,
		Disconnect,
		_Count,
	}

	enum StatusPaneId {
		RemoteAddress,
		PortName,
		State,
		_Count,
	}

	enum InfoId {
		Dsr,
		Cts,
		Dtr,
		Rts,
		BreakCondition,
		_Count,
	}

protected:
	// serial port

	iox.SshChannel m_ssh;
	io.HwcProtoDecoder m_decoder;
	string_t m_portName;
	uint_t bindable m_baudRate;
	uint_t m_dataBits;
	io.SerialParity m_parity;
	io.SerialStopBits m_stopBits;
	io.SerialFlowControl m_flowControl;
	io.SerialStatusLines bindable m_statusLines;
	bool bindable m_dtr;
	bool bindable m_rts;
	bool bindable m_breakCondition;

	// toolbar, status bar & menu

	ui.ComboBox* m_addressCombo;
	ui.ComboBox* m_portCombo;
	ui.Action* m_actionTable[ActionId._Count];
	ui.Icon* m_iconTable[IconId._Count];
	ui.StatusPane* m_statusPaneTable[StatusPaneId._Count];

	// serial property grid

	ui.SocketPropertySet m_socketPropertySet;
	ui.SshPropertySet m_sshPropertySet;
	ui.SerialUi m_serialUi;
	ui.ComboProperty* m_portProp;
	ui.BufferPropertySet m_bufferPropertySet;

	// information grid

	ui.InformationValue* m_infoTable[InfoId._Count];
	ui.StdSessionInfoSet m_stdSessionInfoSet;
	ui.ThroughputInfoSet m_throughputInfoSet;
	ui.ChecksumInfoSet m_checksumInfoSet;

	// ui reactor

	reactor m_uiReactor;

public:
	construct(doc.PluginHost* pluginHost);

	bool errorcode connect();
	void disconnect();

	override void load(doc.Storage* storage);
	override void save(doc.Storage* storage);
	override void updateProperties();
	override bool errorcode applyProperties();
	override void restoreDefaultProperties();

	override size_t errorcode transmit(
		void const* p,
		size_t size
	);

	override bool errorcode connectSession() {
		return m_sessionConnectionState ? true : connect();
	}

	override void disconnectSession() {
		disconnect();
	}

protected:
	void onConnect() {
		m_ssh.m_state ? disconnect() : try connect();
	}

	void onComboEnter() {
		try connect();
	}

	void onBaudRateChanged();
	void onDataBitsChanged();
	void onParityChanged();
	void onStopBitsChanged();
	void onFlowControlChanged();
	void onDtrChanged();
	void onRtsChanged();
	void onBreakConditionChanged();

	void onConnected() {
		m_stdSessionInfoSet.startSession(m_pluginHost.m_mainThreadScheduler);
	}

	void onDisconnected();

	void onIncomingData(
		void const* p,
		size_t size
	) {
		m_decoder.write(p, size);
	}

	void onLoginDlgCompleted(
		string_t userName,
		ui.AuthenticationMethod authenticationMethod,
		string_t privateKeyFileName
	);

	void onMsg(io.HwcMsgHdr const* hdr);

	void onOobData(
		void const* p,
		size_t size
	) {
		m_logWriter.write(log.StdRecordCode.Bin, p, size);
	}

	void createUi();

	iox.SshChannel.ConnectParams const* createConnectParams();
	bool errorcode writeSerialConfigMsg();

	bool errorcode writeLineChangeMsg(
		io.HwcMsgCode code,
		bool value
	);

	void updateLineInfoValue(
		InfoId infoId,
		bool line
	);
}

// . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

SshSerialSession.construct(doc.PluginHost* pluginHost) {
	basetype.construct(pluginHost);
	m_ssh.construct(pluginHost);

	m_ssh.m_onLoginDlgCompleted += onLoginDlgCompleted;
	m_ssh.m_onConnected += onConnected;
	m_ssh.m_onDisconnected += onDisconnected;
	m_ssh.m_onIncomingData += onIncomingData;
	m_decoder.m_onMsg += onMsg;
	m_decoder.m_onOobData += onOobData;

	createUi();

	pluginHost.m_log.addSelectionProcessor(log.ThroughputCalcRangeProcessor.factory~(m_throughputInfoSet));
	pluginHost.m_log.addSelectionProcessor(log.ChecksumCalcRangeProcessor.factory~(m_checksumInfoSet));
}

void SshSerialSession.createUi() {
	m_iconTable[IconId.Connect] = new ui.Icon("images/connect.png");
	m_iconTable[IconId.Disconnect] = new ui.Icon("images/pause.png");

	// toolbar

	m_pluginHost.m_toolBar.addLabel("Address:");
	m_addressCombo = m_pluginHost.m_toolBar.addComboBox(-1);
	m_addressCombo.m_isEditable = true;
	m_addressCombo.m_onReturnPressed += onComboEnter;
	m_addressCombo.m_placeholderText = "Enter a remote IP or hostname...";

	m_pluginHost.m_toolBar.addSpacing();
	m_pluginHost.m_toolBar.addLabel("Port:");
	m_portCombo = m_pluginHost.m_toolBar.addComboBox(-1);
	m_portCombo.m_isEditable = true;
	m_portCombo.m_onReturnPressed += onComboEnter;
	m_portCombo.m_placeholderText = "Enter a serial port name...";

	m_actionTable[ActionId.Connect] = createAction("Connect", m_iconTable [IconId.Connect]);
	m_actionTable[ActionId.Connect].m_onTriggered += onConnect;

	// property grid

	ui.GroupProperty* groupProp = m_pluginHost.m_propertyGrid.createGroupProperty(
		"TCP connection",
		"TCP-specific connection settings"
	);

	m_socketPropertySet.createProperties(m_pluginHost.m_propertyGrid, groupProp);
	m_socketPropertySet.m_defaultRemotePort = Defaults.ServerPort;

	groupProp = m_pluginHost.m_propertyGrid.createGroupProperty(
		"SSH connection",
		"SSH-specific connection settings"
	);

	m_sshPropertySet.createProperties(
		m_pluginHost.m_propertyGrid,
		groupProp,,
		ui.SshPropertySet.PropertyId.UserName |
		ui.SshPropertySet.PropertyId.AuthenticationMethod |
		ui.SshPropertySet.PropertyId.RememberLastPassword
	);

	// settings

	groupProp = m_pluginHost.m_propertyGrid.createGroupProperty(
		"Serial port",
		"Serial port-specific connection settings"
	);

	m_portProp = m_pluginHost.m_propertyGrid.createComboProperty(
		groupProp,,
		"Port",
		"Enter a serial port name...",
		"Specify a serial port name to use"
	);

	ui.FormLayout layout;
	m_serialUi.createForm(layout);
	m_serialUi.createProperties(m_pluginHost.m_propertyGrid, groupProp);

	ui.GroupBox groupBox("Serial settings", layout);
	m_pluginHost.createForm(groupBox);

	m_serialUi.m_baudRateCombo.m_onReturnPressed += onBaudRateChanged;
	bindingof(m_serialUi.m_baudRateCombo.m_currentIndex) += onBaudRateChanged;
	bindingof(m_serialUi.m_dataBitsCombo.m_currentIndex) += onDataBitsChanged;
	bindingof(m_serialUi.m_parityCombo.m_currentIndex) += onParityChanged;
	bindingof(m_serialUi.m_stopBitsCombo.m_currentIndex) += onStopBitsChanged;
	bindingof(m_serialUi.m_flowControlCombo.m_currentIndex) += onFlowControlChanged;
	m_serialUi.m_dtrAction.m_onTriggered += onDtrChanged;
	m_serialUi.m_rtsAction.m_onTriggered += onRtsChanged;
	m_serialUi.m_breakConditionAction.m_onTriggered += onBreakConditionChanged;

	// status bar (only one pane)

	m_statusPaneTable[StatusPaneId.RemoteAddress] = m_pluginHost.m_statusBar.addPane(, 100);
	m_statusPaneTable[StatusPaneId.RemoteAddress].m_color = ui.StdColor.Blue;
	m_statusPaneTable[StatusPaneId.PortName] = m_pluginHost.m_statusBar.addPane(, 80);
	m_statusPaneTable[StatusPaneId.PortName].m_color = ui.StdColor.Blue;
	m_statusPaneTable[StatusPaneId.State] = m_pluginHost.m_statusBar.addPane(, 80);

	m_bufferPropertySet.createPropertiesGrouped(
		m_pluginHost.m_propertyGrid,,,
		ui.BufferPropertySet.PropertyId.All & ~ui.BufferPropertySet.PropertyId.ReadParallelism
	);

	// information grid

	ui.InformationGroup* infoGroup = m_pluginHost.m_informationGrid.createGroup("Serial over SSH");

	m_stdSessionInfoSet.create(m_pluginHost.m_txRxStats, m_pluginHost.m_informationGrid, infoGroup);

	m_infoTable[InfoId.Dsr] = m_pluginHost.m_informationGrid.createValue(infoGroup,,, "DSR");
	m_infoTable[InfoId.Cts] = m_pluginHost.m_informationGrid.createValue(infoGroup,,, "CTS");
	m_infoTable[InfoId.Dtr] = m_pluginHost.m_informationGrid.createValue(infoGroup,,, "DTR");
	m_infoTable[InfoId.Rts] = m_pluginHost.m_informationGrid.createValue(infoGroup,,, "RTS");
	m_infoTable[InfoId.BreakCondition] = m_pluginHost.m_informationGrid.createValue(infoGroup,,, "BREAK");

	m_throughputInfoSet.createInformationValuesGrouped(m_pluginHost.m_informationGrid);
	m_checksumInfoSet.createInformationValuesGrouped(m_pluginHost.m_informationGrid);

	m_pluginHost.m_propertyGrid.resizeNameColumnToContents();
	m_pluginHost.m_informationGrid.resizeNameColumnToContents();

	m_uiReactor.start();
}

void SshSerialSession.load(doc.Storage* storage) {
	m_addressCombo.loadHistory(storage, "addressHistory");
	m_addressCombo.m_editText = storage.readString("address");
	m_portCombo.loadHistory(storage, "portHistory");
	m_portCombo.m_editText = storage.readString("port");

	m_serialUi.load(storage);
	m_serialUi.load(storage);
	m_socketPropertySet.load(storage);
	m_sshPropertySet.load(storage);
	m_bufferPropertySet.load(storage);
	m_ssh.applyProperties(m_socketPropertySet, m_sshPropertySet, m_bufferPropertySet);
}

void SshSerialSession.save(doc.Storage* storage) {
	m_serialUi.save(storage);
	m_socketPropertySet.save(storage);
	m_sshPropertySet.save(storage);
	m_bufferPropertySet.save(storage);

	m_addressCombo.saveHistory(storage, "addressHistory");
	storage.writeString("address", m_addressCombo.m_editText);
	m_portCombo.saveHistory(storage, "portHistory");
	storage.writeString("port", m_portCombo.m_editText);
}

void SshSerialSession.updateProperties() {
	m_serialUi.updateProperties();
	m_socketPropertySet.m_remoteAddressProp.m_value = m_addressCombo.m_editText;
	m_portProp.m_value = m_portCombo.m_editText;
}

bool errorcode SshSerialSession.applyProperties() {
	m_serialUi.applyProperties();

	m_addressCombo.m_editText = m_socketPropertySet.m_remoteAddressProp.m_value;
	m_portCombo.m_editText = m_portProp.m_value;

	m_dtr = m_serialUi.m_dtrProp.m_value;
	m_rts = m_serialUi.m_rtsProp.m_value;
	m_breakCondition = m_serialUi.m_breakConditionProp.m_value;
	m_ssh.applyProperties(m_socketPropertySet, m_sshPropertySet, m_bufferPropertySet);

	if (m_ssh.m_state != State.Connected)
		return true;

	// collect new values from ui

	uint_t baudRate = m_serialUi.m_baudRate;
	uint_t dataBits = m_serialUi.m_dataBits;
	io.SerialStopBits stopBits = m_serialUi.m_stopBits;
	io.SerialParity parity = m_serialUi.m_parity;
	io.SerialFlowControl flowControl = m_serialUi.m_flowControl;

	bool isConfigChanged;
	bool dtr = m_serialUi.m_dtrProp.m_value;
	bool rts = m_serialUi.m_rtsProp.m_value;
	bool breakCondition = m_serialUi.m_breakConditionProp.m_value;

	if (baudRate != m_baudRate) {
		m_logWriter.write(SerialLogRecordCode.BaudRateChanged, &baudRate, sizeof(baudRate));
		m_baudRate = baudRate;
		isConfigChanged = true;
	}

	if (dataBits != m_dataBits) {
		m_logWriter.write(SerialLogRecordCode.DataBitsChanged, &dataBits, sizeof(dataBits));
		m_dataBits = dataBits;
		isConfigChanged = true;
	}

	if (stopBits != m_stopBits) {
		m_logWriter.write(SerialLogRecordCode.StopBitsChanged, &stopBits, sizeof(stopBits));
		m_stopBits = stopBits;
		isConfigChanged = true;
	}

	if (parity != m_parity) {
		m_logWriter.write(SerialLogRecordCode.ParityChanged, &parity, sizeof(parity));
		m_parity = parity;
		isConfigChanged = true;
	}

	if (flowControl != m_flowControl) {
		m_logWriter.write(SerialLogRecordCode.FlowControlChanged, &flowControl, sizeof(flowControl));
		m_flowControl = flowControl;
		isConfigChanged = true;
	}

	if (isConfigChanged)
		writeSerialConfigMsg();

	if (dtr != m_dtr) {
		m_logWriter.write(SerialLogRecordCode.DtrChanged, &dtr, sizeof(dtr));
		m_dtr = dtr;
		writeLineChangeMsg(io.HwcMsgCode.SerialSetDtr, dtr);
	}

	if (rts != m_rts) {
		m_logWriter.write(SerialLogRecordCode.RtsChanged, &rts, sizeof(rts));
		m_rts = rts;
		writeLineChangeMsg(io.HwcMsgCode.SerialSetRts, rts);
	}

	if (breakCondition != m_breakCondition) {
		m_logWriter.write(SerialLogRecordCode.RtsChanged, &breakCondition, sizeof(breakCondition));
		m_breakCondition = breakCondition;
		writeLineChangeMsg(io.HwcMsgCode.SerialSetDtr, breakCondition);
	}

	return true;

catch:
	m_logWriter.writeLastError(log.StdRecordCode.Error);
	return false;
}

void SshSerialSession.restoreDefaultProperties() {
	m_socketPropertySet.restoreDefaults();
	m_sshPropertySet.restoreDefaults();
	m_serialUi.restoreDefaultProperties();
	m_bufferPropertySet.restoreDefaults();
}

size_t errorcode SshSerialSession.transmit(
	void const* p,
	size_t size
) {
	if (m_ssh.m_state != State.Connected) {
		std.setError("serial port not opened");
		return -1;
	}

	io.HwcMsgHdr hdr;
	hdr.m_stx = 0x02;
	hdr.m_code = io.HwcMsgCode.SerialTransmit;
	hdr.m_dataSize = size;

	m_ssh.write(&hdr, sizeof(hdr));
	size_t result = m_ssh.write(p, size);
	if (result)
		m_logWriter.write(log.StdRecordCode.Tx, p, result);

	return result;
}

bool errorcode SshSerialSession.connect() {
	disconnect();
	startSession();

	if (!m_portCombo.m_currentText) {
		std.setError("port name not specified");
		m_logWriter.writeLastError(log.StdRecordCode.Error);
		return false;
	}

	m_portName = m_portCombo.m_currentText;
	m_baudRate = m_serialUi.m_baudRate;
	m_dataBits = m_serialUi.m_dataBits;
	m_stopBits = m_serialUi.m_stopBits;
	m_parity = m_serialUi.m_parity;
	m_flowControl = m_serialUi.m_flowControl;
	m_dtr = m_serialUi.m_dtr;
	m_rts = m_serialUi.m_rts;
	m_breakCondition = m_serialUi.m_breakCondition;
	m_decoder.reset();

	m_addressCombo.addEditTextToHistory();
	m_portCombo.addEditTextToHistory();
	return m_ssh.connect(m_addressCombo.m_editText, createConnectParams());
}

void SshSerialSession.disconnect() {
	if (!m_ssh.m_state)
		return;

	m_ssh.close();
	m_stdSessionInfoSet.endSession();
}

void SshSerialSession.onLoginDlgCompleted(
	string_t userName,
	ui.AuthenticationMethod authenticationMethod,
	string_t privateKeyFileName
) {
	m_sshPropertySet.m_userNameProp.m_value = userName;
	m_sshPropertySet.m_authenticationMethodProp.m_value = authenticationMethod;
	m_sshPropertySet.m_privateKeyFileProp.m_value = privateKeyFileName;
}

void SshSerialSession.onDisconnected() {
	if (m_decoder.m_buffer.m_size) // show buffered bytes
		m_logWriter.write(log.StdRecordCode.Bin, m_decoder.m_buffer.m_p, m_decoder.m_buffer.m_size);

	if (!m_decoder.m_msgCount) {
		std.setError("'ioninja-hwc' on the target machine failed to initialize");
		m_logWriter.writeLastError(log.StdRecordCode.Error);
	}

	m_stdSessionInfoSet.endSession();
}

void SshSerialSession.onMsg(io.HwcMsgHdr const* hdr) {
	switch (hdr.m_code) {
	case io.HwcMsgCode.Error:
		std.Error const* error = (std.Error const*)(hdr + 1);
		m_logWriter.write(log.StdRecordCode.Error, error, error.m_size);
		disconnect();
		break;

	case io.HwcMsgCode.ErrorString:
		std.setError((char const*)(hdr + 1));
		m_logWriter.writeLastError(log.StdRecordCode.Error);
		disconnect();
		break;

	case io.HwcMsgCode.Rx:
		m_logWriter.write(log.StdRecordCode.Rx, hdr + 1, hdr.m_dataSize);
		break;

	case io.HwcMsgCode.SerialLineError:
		io.SerialLineErrors errors = (io.SerialLineErrors)*(uchar_t const*)(hdr + 1);
		m_logWriter.write(SerialLogRecordCode.LineError, &errors, sizeof(errors));
		break;

	case io.HwcMsgCode.SerialCtsDsr:
		io.SerialStatusLines lines = (io.SerialStatusLines)*(uchar_t const*)(hdr + 1);
		io.SerialStatusLines lineMask = lines ^ m_statusLines;
		if (lineMask) {
			SerialStatusLineChangedParams params = { lines, lineMask }
			m_logWriter.write(SerialLogRecordCode.StatusLineChanged, params, sizeof(params));
			m_statusLines = lines;
		}

		break;
	}
}

void SshSerialSession.onBaudRateChanged() {
	if (m_ssh.m_state != State.Connected)
		return;

	uint_t baudRate = m_serialUi.m_baudRate;
	if (baudRate == m_baudRate)
		return;

	m_logWriter.write(SerialLogRecordCode.BaudRateChanged, &baudRate, sizeof(baudRate));
	m_baudRate = baudRate;
	writeSerialConfigMsg();

catch:
	m_logWriter.writeLastError(log.StdRecordCode.Error);
}

void SshSerialSession.onDataBitsChanged() {
	if (m_ssh.m_state != State.Connected)
		return;

	uint_t dataBits = m_serialUi.m_dataBits;
	if (dataBits == m_dataBits)
		return;

	m_logWriter.write(SerialLogRecordCode.DataBitsChanged, &dataBits, sizeof(dataBits));
	m_dataBits = dataBits;
	writeSerialConfigMsg();

catch:
	m_logWriter.writeLastError(log.StdRecordCode.Error);
}

void SshSerialSession.onParityChanged() {
	if (m_ssh.m_state != State.Connected)
		return;

	io.SerialParity parity = m_serialUi.m_parity;
	if (parity == m_parity)
		return;

	m_logWriter.write(SerialLogRecordCode.ParityChanged, &parity, sizeof(parity));
	m_parity = parity;
	writeSerialConfigMsg();

catch:
	m_logWriter.writeLastError(log.StdRecordCode.Error);
}

void SshSerialSession.onStopBitsChanged() {
	if (m_ssh.m_state != State.Connected)
		return;

	io.SerialStopBits stopBits = m_serialUi.m_stopBits;
	if (stopBits == m_stopBits)
		return;

	m_logWriter.write(SerialLogRecordCode.StopBitsChanged, &stopBits, sizeof(stopBits));
	m_stopBits = stopBits;
	writeSerialConfigMsg();

catch:
	m_logWriter.writeLastError(log.StdRecordCode.Error);
}

void SshSerialSession.onFlowControlChanged() {
	if (m_ssh.m_state != State.Connected)
		return;

	io.SerialFlowControl flowControl = m_serialUi.m_flowControl;
	if (flowControl == m_flowControl)
		return;

	m_logWriter.write(SerialLogRecordCode.FlowControlChanged, &flowControl, sizeof(flowControl));
	m_flowControl = flowControl;
	writeSerialConfigMsg();

catch:
	m_logWriter.writeLastError(log.StdRecordCode.Error);
}

iox.SshChannel.ConnectParams const* SshSerialSession.createConnectParams() {
	static string_t const stopBitsStringTable[] = {
		"1",
		"1.5",
		"2",
	}

	static string_t const parityStringTable[] = {
		"none",
		"odd",
		"even",
		"mark",
		"space",
	}

	static string_t const flowControlStringTable[] = {
		"0",
		"1",
		"0", // xon/xoff not supported
	}

	string_t cmdLine =
		"ioninja-hwc --stdin "
		$"--serial=%1 "
		$"--ser-baud %2 "
		$"--ser-data %3 "
		$"--ser-stop %4 "
		$"--ser-parity %5 "
		$"--ser-flow %6 "
		$"--ser-dtr %7 "
		$"--ser-rts %8 "(
			m_portName,
			m_baudRate,
			m_dataBits,
			stopBitsStringTable[m_stopBits],
			parityStringTable[m_parity],
			flowControlStringTable[m_flowControl],
			m_dtr,
			m_rts
		);

	iox.SshChannel.ConnectParams params;
	iox.SshChannel.initConnectParams(&params, m_socketPropertySet, m_sshPropertySet, m_bufferPropertySet);
	params.m_sshParams.m_channelType = "session";
	params.m_sshParams.m_processType = "exec";
	params.m_sshParams.m_processExtra = cmdLine.m_p;
	params.m_sshParams.m_processExtraSize = cmdLine.m_length;
	return params;
}

bool errorcode SshSerialSession.writeSerialConfigMsg() {
	struct Msg {
		io.HwcMsgHdr m_hdr;
		io.HwcSerialConfig m_config;
	};

	Msg msg;
	msg.m_hdr.m_stx = 0x02;
	msg.m_hdr.m_code = io.HwcMsgCode.SerialSetConfig;
	msg.m_hdr.m_dataSize = sizeof(msg.m_config);
	msg.m_config.m_baudRate = m_baudRate;
	msg.m_config.m_dataBits = m_dataBits;
	msg.m_config.m_stopBits = m_stopBits;
	msg.m_config.m_parity = m_parity;
	msg.m_config.m_flowControl = m_flowControl;
	m_ssh.write(&msg, sizeof(msg));
	return true;
}

bool errorcode SshSerialSession.writeLineChangeMsg(
	io.HwcMsgCode code,
	bool value
) {
	struct Msg {
		io.HwcMsgHdr m_hdr;
		bool m_value;
	};

	Msg msg;
	msg.m_hdr.m_stx = 0x02;
	msg.m_hdr.m_code = code;
	msg.m_hdr.m_dataSize = sizeof(msg.m_value);
	msg.m_value = value;
	m_ssh.write(&msg, sizeof(msg));
	return true;
}

void SshSerialSession.onDtrChanged() {
	if (m_ssh.m_state != State.Connected)
		return;

	bool value = m_serialUi.m_dtr;
	writeLineChangeMsg(io.HwcMsgCode.SerialSetDtr, value);
	m_dtr = value;
	m_logWriter.write(SerialLogRecordCode.DtrChanged, &value, sizeof(value));
}

void SshSerialSession.onRtsChanged() {
	if (m_ssh.m_state != State.Connected)
		return;

	bool value = m_serialUi.m_rts;
	writeLineChangeMsg(io.HwcMsgCode.SerialSetRts, value);
	m_rts = value;
	m_logWriter.write(SerialLogRecordCode.RtsChanged, &value, sizeof(value));
}

void SshSerialSession.onBreakConditionChanged() {
	if (m_ssh.m_state != State.Connected)
		return;

	bool value = m_serialUi.m_breakCondition;
	writeLineChangeMsg(io.HwcMsgCode.SerialSetBreak, value);
	m_breakCondition = value;
	m_logWriter.write(SerialLogRecordCode.BreakConditionChanged, &value, sizeof(value));
}

void SshSerialSession.updateLineInfoValue(
	InfoId infoId,
	bool line
) {
	m_infoTable[infoId].m_value = line ? "on" : "off";
	m_infoTable[infoId].m_valueColor = line ? ui.StdColor.BrightRed : ui.StdColor.BrightBlack;
}

reactor SshSerialSession.m_uiReactor {
	m_isTransmitEnabled = m_ssh.m_isTransmitEnabled;

	m_title = m_ssh.m_state == State.Connected ?
		$"Serial $(m_ssh.m_remoteAddress.getString()) $m_portName $m_baudRate bps" :
		$"Serial $(m_addressCombo.m_currentText) $(m_portCombo.m_currentText) $(m_serialUi.m_baudRateCombo.m_editText)";

	m_sessionConnectionState =
		m_ssh.m_state == State.Connected ? SessionConnectionState.Connected :
		m_ssh.m_state == State.Closed ? SessionConnectionState.Disconnected :
		SessionConnectionState.Connecting;

	m_actionTable[ActionId.Connect].m_text = m_ssh.m_state ? "Close" : "Open";
	m_actionTable[ActionId.Connect].m_icon = m_iconTable[m_ssh.m_state ? IconId.Disconnect : IconId.Connect];
	m_serialUi.m_rtsAction.m_isEnabled = m_serialUi.m_flowControlCombo.m_currentData != io.SerialFlowControl.RtsCts;
	m_serialUi.m_rtsProp.m_isEnabled = m_serialUi.m_flowControlProp.m_value != io.SerialFlowControl.RtsCts;
	m_statusPaneTable[StatusPaneId.State].m_text = m_ssh.m_stateStringTable[m_ssh.m_state];
	m_statusPaneTable[StatusPaneId.RemoteAddress].m_text = m_ssh.m_state > State.Resolving ?
		m_ssh.m_remoteAddress.getString() :
		"<peer-address>";
	m_statusPaneTable[StatusPaneId.RemoteAddress].m_isVisible = m_ssh.m_state > State.Resolving;
	m_statusPaneTable[StatusPaneId.PortName].m_text = m_ssh.m_state == State.Connected ?
		m_portName :
		"<port-name>";
	m_statusPaneTable[StatusPaneId.PortName].m_isVisible = m_ssh.m_state == State.Connected;
	m_infoTable[InfoId.Dsr].m_isEnabled = m_ssh.m_state == State.Connected;
	m_infoTable[InfoId.Cts].m_isEnabled = m_ssh.m_state == State.Connected;
	m_infoTable[InfoId.Dtr].m_isEnabled = m_ssh.m_state == State.Connected;
	m_infoTable[InfoId.Rts].m_isEnabled = m_ssh.m_state == State.Connected;
	m_infoTable[InfoId.BreakCondition].m_isEnabled = m_ssh.m_state == State.Connected;

	updateLineInfoValue(InfoId.Dtr, m_dtr);
	updateLineInfoValue(InfoId.Rts, m_rts);
	updateLineInfoValue(InfoId.Dsr, m_statusLines & io.SerialStatusLines.Dsr);
	updateLineInfoValue(InfoId.Cts, m_statusLines & io.SerialStatusLines.Cts);
	updateLineInfoValue(InfoId.BreakCondition, m_breakCondition);
}

//..............................................................................
