//..............................................................................
//
// Serial log filter
//

import "doc_Plugin.jnc"
import "log_Filter.jnc"
import "ui_ToggleUi.jnc"
import "ui_Layout.jnc"
import "ui_GroupBox.jnc"
import "ui_Button.jnc"

//..............................................................................

class SerialLogFilter:
	doc.LogFilter,
	log.Filter {
protected:
	log.Log* m_log;

	ui.ToggleUi m_serialSettingChangeFilter;
	ui.ToggleUi m_controlLineChangeFilter;
	ui.ToggleUi m_statusLineChangeFilter;
	ui.ToggleUi m_lineErrorFilter;

	char const Title[] = "Serial log filter";

public:
	construct(doc.PluginHost* pluginHost);

	override void load(doc.Storage* storage);
	override void save(doc.Storage* storage);
	override void updateProperties();
	override bool errorcode applyProperties();
	override void restoreDefaultProperties();

	override bool filter(
		uint64_t timestamp,
		uint64_t recordCode,
		void const* p,
		size_t size
	);

protected:
	void onRebuildLog() {
		m_log.rebuild();
	}
}

// . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

SerialLogFilter.construct(doc.PluginHost* pluginHost) {
	m_log = pluginHost.m_log;

	ui.VBoxLayout layout;
	ui.GroupBox groupBox(Title, layout);

	ui.Action applyAction("images/apply.png", "Rebuild log", onRebuildLog);
	ui.ToolBar applyToolBar;
	applyToolBar.addAction(applyAction);

	ui.GroupProperty* groupProp = pluginHost.m_propertyGrid.createGroupProperty(Title);

	m_serialSettingChangeFilter.create(
		layout,,
		groupProp,
		"Serial setting changes",
		"Show/hide serial setting changes (baud rate, data bits, etc.)",
		true
	);

	m_controlLineChangeFilter.create(
		layout,,
		groupProp,
		"DTR/RTS changes",
		"Show/hide serial control lines changes (DTR/RTS)",
		true
	);

	m_statusLineChangeFilter.create(
		layout,,
		groupProp,
		"DSR/CTS/DCD/RI changes",
		"Show/hide serial control lines changes (DSR/CTS/DCD/RI)",
		true
	);

	m_lineErrorFilter.create(
		layout,
		applyToolBar,
		groupProp,
		"Serial line errors",
		"Show/hide serial line errors (FRAMING, PARITY, BREAK)",
		true
	);

	pluginHost.createForm(groupBox);

	pluginHost.m_log.addFilter(this);
}

void SerialLogFilter.load(doc.Storage* storage) {
	m_serialSettingChangeFilter := storage.readBool("serial-setting", true);
	m_controlLineChangeFilter := storage.readBool("serial-control", true);
	m_statusLineChangeFilter := storage.readBool("serial-status", true);
	m_lineErrorFilter := storage.readBool("serial-line-error", true);
}

void SerialLogFilter.save(doc.Storage* storage) {
	storage.writeBool("serial-setting", m_serialSettingChangeFilter.m_value);
	storage.writeBool("serial-control", m_controlLineChangeFilter.m_value);
	storage.writeBool("serial-status", m_statusLineChangeFilter.m_value);
	storage.writeBool("serial-line-error", m_lineErrorFilter.m_value);
}

void SerialLogFilter.updateProperties() {
	m_serialSettingChangeFilter.update();
	m_controlLineChangeFilter.update();
	m_statusLineChangeFilter.update();
	m_lineErrorFilter.update();
}

bool errorcode SerialLogFilter.applyProperties() {
	m_serialSettingChangeFilter.apply();
	m_controlLineChangeFilter.apply();
	m_statusLineChangeFilter.apply();
	m_lineErrorFilter.apply();
	return true;
}

void SerialLogFilter.restoreDefaultProperties() {
	m_serialSettingChangeFilter := true;
	m_controlLineChangeFilter := true;
	m_statusLineChangeFilter := true;
	m_lineErrorFilter := true;
}

// . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

bool SerialLogFilter.filter(
	uint64_t timestamp,
	uint64_t recordCode,
	void const* p,
	size_t size
) {
	switch (recordCode) {
	case SerialLogRecordCode.BaudRateChanged:
	case SerialLogRecordCode.DataBitsChanged:
	case SerialLogRecordCode.StopBitsChanged:
	case SerialLogRecordCode.ParityChanged:
	case SerialLogRecordCode.FlowControlChanged:
	case SerialLogRecordCode.ReadModeChanged:
	case SerialLogRecordCode.ReadIntervalChanged:
		return m_serialSettingChangeFilter.m_value;

	case SerialLogRecordCode.ControlLineChanged:
	case SerialLogRecordCode.RtsChanged:
	case SerialLogRecordCode.DtrChanged:
	case SerialLogRecordCode.BreakConditionChanged:
		return m_controlLineChangeFilter.m_value;

	case SerialLogRecordCode.StatusLineChanged:
		return m_statusLineChangeFilter.m_value;

	case SerialLogRecordCode.LineError:
		return m_lineErrorFilter.m_value;

	default:
		return true;
	}
}

//..............................................................................
