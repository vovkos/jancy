//..............................................................................
//
// Ninja log - writer
//

import "log_RecordCode.jnc"

namespace log {

//! \addtogroup api-log
//! @{

//..............................................................................

opaque class Writer {
	void write(
		uint64_t timestamp,
		uint64_t recordCode,
		void const* p,
		size_t size
	);

	// when jnccdecl vararg is functional, we can remove this overload

	void write(
		uint64_t timestamp,
		uint64_t recordCode,
		void const* p1,
		size_t size1,
		void const* p2,
		size_t size2
	);

	void write(
		uint64_t recordCode,
		void const* p,
		size_t size
	) {
		write(sys.getPreciseTimestamp(), recordCode, p, size);
	}

	void write(
		uint64_t recordCode,
		void const* p1,
		size_t size1,
		void const* p2,
		size_t size2
	) {
		write(sys.getPreciseTimestamp(), recordCode, p1, size1, p2, size2);
	}

	void write(
		uint64_t timestamp,
		uint64_t recordCode
	) {
		write(timestamp, recordCode, null, 0);
	}

	void write(uint64_t recordCode) {
		write(sys.getPreciseTimestamp(), recordCode, null, 0);
	}

	void write(
		uint64_t timestamp,
		uint64_t recordCode,
		string_t text
	) {
		write(timestamp, recordCode, text.m_p, text.m_length);
	}

	void write(
		uint64_t recordCode,
		string_t text
	) {
		write(sys.getPreciseTimestamp(), text.m_p, text.m_length);
	}

	void write(string_t text) {
		write(sys.getPreciseTimestamp(), log.StdRecordCode.PlainText, text.m_p, text.m_length);
	}

	void write(
		uint64_t timestamp,
		uint64_t recordCode,
		std.Error const* error
	) {
		write(timestamp, recordCode, error, error.m_size);
	}

	void write(
		uint64_t recordCode,
		std.Error const* error
	) {
		write(sys.getPreciseTimestamp(), recordCode, error, error.m_size);
	}

	void writeLastError(
		uint64_t timestamp,
		uint64_t recordCode
	) {
		write(timestamp, recordCode, std.getLastError());
	}

	void writeLastError(uint64_t recordCode) {
		write(sys.getPreciseTimestamp(), recordCode, std.getLastError());
	}

	void retroColorize(
		intptr_t retroOffset,
		size_t length,
		ui.TextAttr attr,
		RetroColorizeFlags flags = 0
	) {
		RetroColorizeParams params;
		params.m_retroOffset = retroOffset;
		params.m_length = length;
		params.m_attr = attr;
		params.m_flags = flags;

		write(StdRecordCode.RetroColorize, &params, sizeof(params));
	}

	void retroColorize(
		intptr_t retroOffset,
		size_t length,
		uint_t color
	) {
		ui.TextAttr attr;
		attr.m_textColor = color;
		attr.m_backColor = ui.ColorFlags.Undefined;

		retroColorize(retroOffset, length, attr);
	}

	void sync(uint64_t syncId) {
		write(StdRecordCode.SyncId, &syncId, sizeof(syncId));
	}
}

//..............................................................................

class ProxyWriter: Writer {
	Writer* autoget property m_target;
}

//..............................................................................

//! @}

} // namespace log
