//..............................................................................
//
// Ninja UI - actions (QT-style model)
//

import "ui_Icon.jnc"

namespace ui {

//! \addtogroup api-ui
//! @{

//..............................................................................

opaque class Action {
	construct(
		Icon* icon,
		char const* text,
		void function* onTriggered() = null
	) {
		initialize(icon, text, onTriggered);
	}

	construct(
		char const* iconFileName,
		char const* text,
		void function* onTriggered() = null
	) {
		initialize(new Icon(iconFileName), text, onTriggered);
	}

	construct(
		char const* text,
		void function* onTriggered() = null
	) {
		initialize(null, text, onTriggered);
	}

	bool autoget property m_isVisible;
	bool autoget property m_isEnabled;
	bool autoget property m_isCheckable;
	bool bindable autoget property m_isChecked;

	char const* autoget property m_text;
	Icon* autoget property m_icon;

	event m_onTriggered();

protected:
	void initialize(
		Icon* icon,
		char const* text
	);

	void initialize(
		Icon* icon,
		char const* text,
		void function* onTriggered()
	) {
		initialize(icon, text);
		if (onTriggered)
			m_onTriggered += onTriggered;
	}
}

//..............................................................................

//! @}

} // namespace ui
