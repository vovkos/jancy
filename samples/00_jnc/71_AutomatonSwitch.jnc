//..............................................................................
//
//  This file is part of the Jancy toolkit.
//
//  Jancy is distributed under the MIT license.
//  For details see accompanying license.txt file,
//  the public copy of which is also available at:
//  http://tibbo.com/downloads/archive/jancy/license.txt
//
//..............................................................................

// Like Ragel, Jancy-generated recognizer support mixed-language documents.
// Developer can switch languages at will, by adjusting the value of
// jnc.Recognizer.m_automatonFunc field at appropriate locations.

//..............................................................................

// the first language recognizes lexeme 'foo'

jnc.AutomatonResult automaton foo (jnc.Recognizer* recognizer)
{
	%% "foo"
		printf ("lexeme found: foo\n");

	%% '\''
		recognizer.m_automatonFunc = bar; // switch language

	%% \s+
		// ignore whitespace
}

// the second language recognizes lexeme 'bar'

jnc.AutomatonResult automaton bar (jnc.Recognizer* recognizer)
{
	%% "bar"
		printf ("lexeme found: bar\n");

	%% "\\'"
		// ignore escape-protected apostrophe

	%% '\''
		recognizer.m_automatonFunc = foo; // switch language back

	%% \s+
		// ignore whitespace
}

//..............................................................................

// entry point

int main ()
{
	printf ("main ()\n");

	jnc.Recognizer recognizer (foo);

	bool result = try recognizer.recognize ("foo 'bar \\' bar' foo");
	if (!result)
		printf ("recognition error: %s\n", std.getLastError ().m_description);

	return 0;
}

//..............................................................................
