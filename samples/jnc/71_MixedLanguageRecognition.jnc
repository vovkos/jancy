//..............................................................................
//
//  This file is part of the Jancy toolkit.
//
//  Jancy is distributed under the MIT license.
//  For details see accompanying license.txt file,
//  the public copy of which is also available at:
//  http://tibbo.com/downloads/archive/jancy/license.txt
//
//..............................................................................

// Like Ragel, Jancy-generated recognizers can be used for processing
// mixed-language documents. Developer can switch languages at will and just
// feed the data to the appropriate 'switch'.

//..............................................................................

// entry point

int main() {
	printf("main ()\n");

	enum Language {
		Foo, // the first language recognizes lexeme 'foo'
		Bar  // the second language recognizes lexeme 'bar'
	}

	Language language = Language.Foo;

	string_t text = r" foooo 2 barr \1 barrr 1 foooooo \2 fooo ";
	while (text.m_length) {
		switch (language) {
		case Language.Foo:
			[ RegexAnchored ]
			switch (text) {
			case "foo+":
				printf($"foo: $0\n");
				break;

			case "2":
				language = Language.Bar; // switch language to Bar
				break;

			case r"\\2":
				// ignore if it's escape-protected
				break;

			case r"\s+":
				// ignore whitespace
				break;

			default:
				printf("the Foo language recognition error at %s\n", text);
				return -1;
			}

			text += $0.m_length; // advance to the next lexeme
			break;

		case Language.Bar:
			[ RegexAnchored ]
			switch (text) {
			case "bar+":
				printf($"bar: $0\n");
				break;

			case "1":
				language = Language.Foo; // switch language back to Foo
				break;

			case r"\\1":
				// ignore if it's escape-protected
				break;

			case r"\s+":
				// ignore whitespace
				break;

			default:
				printf("the Bar language recognition error at: %s\n", text);
				return -2;
			}

			text += $0.m_length; // advance to the next lexeme
			break;
		}
	}

	return 0;
}

//..............................................................................
