//..............................................................................
//
//  This file is part of the Jancy toolkit.
//
//  Jancy is distributed under the MIT license.
//  For details see accompanying license.txt file,
//  the public copy of which is also available at:
//  http://tibbo.com/downloads/archive/jancy/license.txt
//
//..............................................................................

import "std_MapEntry.jnc"

namespace std {

//! \addtogroup std-container
//! @{

//..............................................................................

typedef int CmpFunc(
	variant_t key1,
	variant_t key2
);

// . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

//! \import std_RbTree.jnc

opaque class RbTree {
	MapEntry cmut* readonly m_head;
	MapEntry cmut* readonly m_tail;
	size_t readonly m_count;

	construct(CmpFunc thin* cmpFunc = null);

	destruct();

	bool const property m_isEmpty {
		return m_count == 0;
	}

	variant_t get(variant_t key) {
		return findValue(key, null);
	}

	void set(
		variant_t key,
		variant_t value
	) {
		add(key, value);
	}

	void clear();

	MapEntry* errorcode visit(variant_t key);
	MapEntry cmut* find(variant_t key) const;

	variant_t findValue(
		variant_t key,
		variant_t undefinedValue = null
	) const {
		MapEntry* entry = find(key);
		return entry ? entry.m_value : undefinedValue;
	}

	MapEntry* errorcode add(
		variant_t key,
		variant_t value
	) {
		MapEntry* entry = visit(key);
		entry.m_value = value;
		return entry;
	}

	void remove(MapEntry* entry);

	bool removeKey(variant_t key) {
		MapEntry* entry = find(key);
		if (!entry)
			return false;

		remove(entry);
		return true;
	}
}

//..............................................................................

//! @}

} // namespace std
