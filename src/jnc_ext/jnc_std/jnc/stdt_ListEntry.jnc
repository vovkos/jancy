//..............................................................................
//
//  This file is part of the Jancy toolkit.
//
//  Jancy is distributed under the MIT license.
//  For details see accompanying license.txt file,
//  the public copy of which is also available at:
//  http://tibbo.com/downloads/archive/jancy/license.txt
//
//..............................................................................

namespace stdt {

//! \addtogroup std-container
//! @{

//..............................................................................

struct ListEntry<T> {
	friend List;
	T cmut* readonly m_next;
	T cmut* readonly m_prev;
}

// . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

struct IteratorBase<T> {
	typedef T Entry;

	T* readonly m_p;

	T* operator * () thin const {
		return m_p;
	}

	T* operator -> () thin const {
		return m_p;
	}
}

// . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

struct IteratorImpl<T>: T {
	construct(T.Entry* p = null) thin {
		m_p = p;
	}

	operator bool () thin const {
		return m_p != null;
	}

	IteratorImpl operator ++ () thin {
		m_p = m_p.m_next;
		return *this;
	}

	IteratorImpl postfix operator ++ () thin {
		IteratorImpl saved = *this;
		m_p = m_p.m_next;
		return saved;
	}

	IteratorImpl operator -- () thin {
		m_p = m_p.m_prev;
		return *this;
	}

	IteratorImpl postfix operator -- () thin {
		IteratorImpl saved = *this;
		m_p = m_p.m_prev;
		return saved;
	}
}

// . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

typedef IteratorImpl<IteratorBase<T> > Iterator<T>;

//..............................................................................

struct BoxListEntry<T>: ListEntry<BoxListEntry> {
	typedef T Value;

	T m_value;

	construct() {}

	construct(T value) {
		m_value = value;
	}
}

// . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

struct BoxIteratorBase<T>: IteratorBase<BoxListEntry<T> > {
	T operator * () thin const {
		return m_p.m_value;
	}

	T* operator -> () thin const {
		return &m_p.m_value;
	}

	T.Value operator := (T.Value value) thin const {
		return m_p.m_value = value;
	}
}

// . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

typedef IteratorImpl<BoxIteratorBase<T> > BoxIterator<T>;

//..............................................................................

struct MapEntryBase<T, K, V>: ListEntry<T> {
	typedef K Key;
	typedef V Value;

	K readonly m_key;
	V m_value;
}

//..............................................................................

//! @}

} // namespace stdt
